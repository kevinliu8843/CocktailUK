<script language="VBScript" RUNAT="Server">

	Sub Application_OnStart
		Application("ActiveUsers") = 0
	End Sub
    
	Sub Session_OnStart
	   	'General Server Vars - vary these to suit
		Session.Timeout = 5
	
	   	CONST BASKET_COLUMNS = 20
		Dim aryBasket()
		ReDim aryBasket(BASKET_COLUMNS, -1)
		Session("basket") 		= aryBasket
		Session("numberItems") 		= -1
		Session("valueItems")  		= 0
		Session("totalNumberItems") 	= 0
		Session("dontCheckAffiliate") 	= True
		Session("checkBasket") 		= True
 
		call SetupSearchReferer(cnGlobal)

		'Current users Stuff
		Application.Lock
			Application("ActiveUsers") = Application("ActiveUsers") + 1
		Application.UnLock
	End Sub
    
	Sub Session_OnEnd
		Application.Lock
			Application("ActiveUsers") = Application("ActiveUsers") - 1
		Application.UnLock
	End Sub

	Sub SetupSearchReferer(cnGlobal)
		Dim aryEngines(13,2), oregexp, strPattern, strEngine, strQuery, j, strReferer, blnTest, omatch
	
		strReferer = Trim(Request.ServerVariables("HTTP_REFERER")&"")
		aryEngines(0,0) = "Google Adwords"
		aryEngines(0,1) = "google[\w.]*/url"
		aryEngines(0,2) = "q"
		
		aryEngines(1,0) = "Google Adwords"
		aryEngines(1,1) = "googleadservices."
		aryEngines(1,2) = "adurl"
		
		aryEngines(2,0) = "Lycos"
		aryEngines(2,1) = "lycos."
		aryEngines(2,2) = "query"
		
		aryEngines(3,0) = "Altavista"
		aryEngines(3,1) = "altavista."
		aryEngines(3,2) = "q"
		
		aryEngines(4,0) = "Espotting"
		aryEngines(4,1) = "espotting."
		aryEngines(4,2) = "keyword"
		
		aryEngines(5,0) = "Ask Jeeves"
		aryEngines(5,1) = ".ask"
		aryEngines(5,2) = "q"
		
		aryEngines(6,0) = "Google"
		aryEngines(6,1) = "google."
		aryEngines(6,2) = "&q"

		aryEngines(7,0) = "MSN"
		aryEngines(7,1) = "msn."
		aryEngines(7,2) = "q"

		aryEngines(8,0) = "Yahoo!"
		aryEngines(8,1) = "yahoo."
		aryEngines(8,2) = "p"

		aryEngines(9,0) = "AOL"
		aryEngines(9,1) = "aol."
		aryEngines(9,2) = "query"

		aryEngines(10,0) = "Excite"
		aryEngines(10,1) = "excite."
		aryEngines(10,2) = "qkw"

		aryEngines(11,0) = "Excite"
		aryEngines(11,1) = "excite."
		aryEngines(11,2) = "s"
		
		aryEngines(12,0) = "All The Web"
		aryEngines(12,1) = "alltheweb."
		aryEngines(12,2) = "q"

		aryEngines(13,0) = "NBCI"
		aryEngines(13,1) = "nbci"
		aryEngines(13,2) = "qkw"

		strEngine = ""
		strQuery = ""
		
		If strReferer <> "" Then
			Set oregexp = New RegExp
			oregexp.IgnoreCase = True
			strPattern = "=([^&]+)&?"
			For j=0 to UBound(aryEngines,1)
				oregexp.pattern = aryEngines(j,1)
				blnTest = oregexp.test(strReferer)
				if blnTest and strEngine = "" Then
					strEngine = aryEngines(j,0)
					oregexp.pattern = aryEngines(j,2) & strPattern
					For each omatch in oregexp.Execute(strReferer)
						strQuery = formatString(omatch.SubMatches(0))
					Next
				End If
			Next
			Set oregexp = Nothing
		End If
		If strReferer <> "" Then
			Session("searchenginereferer") = strReferer
		End If
		If strEngine <> "" Then
			Session("searchengine") = strEngine 
			Session("keywords") = strQuery
		End If
	End Sub

	Function formatString(strString)
		dim strString2
		strString2 = Replace(strString, "+", " ")
		formatString = Trim(strString2)
	End Function
</script>
